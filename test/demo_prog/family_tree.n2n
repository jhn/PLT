member: Node = {Name: String, Age: Int};
relation: Rel = {Relation: String};

fn DFS_setup(g: Graph) -> Graph {
	g.map(node in { node [+] visited: Bool; });
	return g;
}

fn DFS(N: Node, M: Graph)->Int {
	if(N.visited == false){
		print(N);
		print("\n");
		Neighbor: List<Node> = M.neighbors(N);
		N.visited = true;
		Neighbor.map(node in { DFS(node, M); });
	}
	return 0;
}

fn main()->Void {
	John: Node = member["John Hamilton", 49];
	Mary: Node = member["Mary Lance", 47];
	Johan: Node = member["Johan Hamilton", 21];
	Sara: Node = member["Sara Hamilton", 20];

	Mother: Rel = relation["Mother of"];
	Father: Rel = relation["Father of"];
	Child: Rel = relation["Child of"];
	Brother: Rel = relation["Brother of"];
	Sister: Rel = relation["Sister of"];

	Family: Graph = <
		John Father Johan,
		John Father Sara,
		Mary Mother Johan,
		Mary Mother Sara,
		Johan Brother Sara,
		Johan Child John,
		Johan Child Mary,
		Sara Sister Johan,
		Sara Child John,
		Sara Child Mary
	>;

	family_setup: Graph = DFS_setup(Family);
	DFS(Johan, family_setup);
}
